# Helper functions for pam-access tests

package pam_lib;

use strict;
use warnings;
use IO::String;
use JSON;

# Install sibling plugin templates into LLNG site/templates/
sub install_plugin_templates {
    require File::Find;
    require File::Copy;
    require File::Path;
    require FindBin;

    my $plugins_root = "$FindBin::Bin/../../";
    for my $plugin (qw(oidc-device-authorization pam-access)) {
        my $tpl_dir = "$plugins_root/$plugin/portal-templates";
        next unless -d $tpl_dir;
        File::Find::find(
            {
                wanted => sub {
                    return unless -f $_ && /\.tpl$/;
                    my $rel = $File::Find::name;
                    $rel =~ s{^\Q$tpl_dir/\E}{};
                    my $dst = "site/templates/$rel";
                    File::Path::make_path( $dst =~ s{/[^/]+$}{}r );
                    File::Copy::copy( $File::Find::name, $dst );
                },
                no_chdir => 1,
            },
            $tpl_dir
        );
    }
}

# Enroll a server via Device Authorization Grant
# Returns the server access_token
sub enroll_server {
    my ( $op, $user_session_id ) = @_;

    # Initiate device authorization
    my $query =
      'client_id=pam-access&client_secret=pamsecret&scope=pam:server';
    my $res = $op->_post(
        '/oauth2/device',
        IO::String->new($query),
        accept => 'application/json',
        length => length($query),
    );
    die "Device auth failed: $res->[0]" unless $res->[0] == 200;

    my $json      = JSON::from_json( $res->[2]->[0] );
    my $device_code = $json->{device_code};
    my $user_code   = $json->{user_code};
    $user_code =~ s/-//g;

    # Get verification page for CSRF token
    $res = $op->_get(
        '/device',
        query  => "user_code=$user_code",
        cookie => "lemonldap=$user_session_id",
        accept => 'text/html',
    );
    my ($csrf_token) =
      $res->[2]->[0] =~ m/name="token"\s+value="([^"]+)"/;

    # Approve device
    $query = main::buildForm( {
            user_code => $user_code,
            action    => 'approve',
            token     => $csrf_token,
        }
    );
    $res = $op->_post(
        '/device',
        IO::String->new($query),
        cookie => "lemonldap=$user_session_id",
        accept => 'text/html',
        length => length($query),
    );

    # Exchange device code for token
    $query =
        "grant_type=urn:ietf:params:oauth:grant-type:device_code"
      . "&device_code=$device_code"
      . "&client_id=pam-access"
      . "&client_secret=pamsecret";
    $res = $op->_post(
        '/oauth2/token',
        IO::String->new($query),
        accept => 'application/json',
        length => length($query),
    );
    die "Token exchange failed: $res->[0]" unless $res->[0] == 200;

    $json = JSON::from_json( $res->[2]->[0] );
    return $json->{access_token};
}

# Common OIDC+PamAccess config for tests
sub base_config {
    return (
        authentication                  => 'Demo',
        userDB                          => 'Same',
        issuerDBOpenIDConnectActivation => 1,
        customPlugins =>
          '::Plugins::PamAccess ::Plugins::OIDCDeviceAuthorization',
        oidcRPMetaDataExportedVars => {
            'pam-access' => {
                email  => 'mail',
                name   => 'cn',
                groups => 'groups',
            }
        },
        oidcServiceMetaDataAuthorizeURI       => 'authorize',
        oidcServiceMetaDataCheckSessionURI    => 'check_session',
        oidcServiceMetaDataJWKSURI            => 'jwks',
        oidcServiceMetaDataEndSessionURI      => 'logout',
        oidcServiceMetaDataRegistrationURI    => 'register',
        oidcServiceMetaDataTokenURI           => 'token',
        oidcServiceMetaDataUserInfoURI        => 'userinfo',
        oidcServiceMetaDataIntrospectionURI   => 'introspect',
        oidcServiceAllowAuthorizationCodeFlow => 1,
        oidcRPMetaDataOptions                 => {
            'pam-access' => {
                oidcRPMetaDataOptionsDisplayName          => 'PAM Access',
                oidcRPMetaDataOptionsClientID             => 'pam-access',
                oidcRPMetaDataOptionsClientSecret         => 'pamsecret',
                oidcRPMetaDataOptionsAccessTokenExpiration => 600,
                oidcRPMetaDataOptionsAllowDeviceAuthorization => 1,
            }
        },
        oidcRPMetaDataScopeRules => {
            'pam-access' => {
                pam          => '1',
                'pam:server' => '1',
            }
        },
        oidcOPMetaDataOptions    => {},
        oidcOPMetaDataJSON       => {},
        oidcOPMetaDataJWKS       => {},
        oidcServicePrivateKeySig => main::oidc_key_op_private_sig(),
        oidcServicePublicKeySig  => main::oidc_cert_op_public_sig(),
        pamAccessActivation      => 1,
        pamAccessRp              => 'pam-access',
    );
}

1;
